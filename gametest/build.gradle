//file:noinspection All

plugins {
    id 'fabric-loom'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = project(':').file("src/main/resources/pistonlib.accesswidener")
    runs {
        gametest {
            server()
            name "Game Test"
            vmArg "-Dfabric-api.gametest"
            vmArg "-Dfabric-api.gametest.server=true"
            vmArg "-Dfabric-api.gametest.command=true"
            vmArg "-Dgametestlib.keepAlive=true"
            runDir "build/gametest"
        }
    }
}

test.dependsOn runGametest

repositories {
    allprojects {
        repositories {
            maven {
                name 'JitPack'
                url 'https://jitpack.io'
            }
        }
    }
    mavenCentral()
}


sourceSets {
    main {
        resources {
            srcDir "src/generated/resources"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Toml
    modImplementation "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
    include "com.moandjiezana.toml:toml4j:${project.toml4j_version}"

    // Lombok
    compileOnly(annotationProcessor("org.projectlombok:lombok:${project.lombok_version}"))

    // MixinExtras
    include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixinextras_Version}")))

    implementation project(path: ":", configuration: "namedElements")

    // GameTestLib
    modImplementation("com.github.fxmorin:GameTestLib:cb4f041d45")
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

repositories {}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {}
}
